plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.google.gms.google.services)
    alias(libs.plugins.spotless)
}

android {
    namespace 'com.gege.activityfindermobile'
    compileSdk 36

    defaultConfig {
        applicationId "com.gege.activityfindermobile"
        minSdk 26
        targetSdk 36
        versionCode 10
        versionName "1.09 alpha"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Read API key from local.properties
        Properties properties = new Properties()
        if (rootProject.file("local.properties").exists()) {
            properties.load(rootProject.file("local.properties").newDataInputStream())
        }
        manifestPlaceholders = [GOOGLE_MAPS_API_KEY: properties.getProperty("GOOGLE_MAPS_API_KEY", "")]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.firebase.messaging
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.test.core
    androidTestImplementation libs.test.rules
    androidTestImplementation libs.test.runner
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.espresso.contrib
    androidTestImplementation libs.espresso.intents
    debugImplementation libs.fragment.testing

    // Navigation Component
    implementation libs.navigation.fragment
    implementation libs.navigation.ui

    // Lifecycle (ViewModel, LiveData)
    implementation libs.lifecycle.viewmodel
    implementation libs.lifecycle.livedata

    // Retrofit (Networking)
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.okhttp
    implementation libs.logging.interceptor

    // Hilt (Dependency Injection)
    implementation libs.hilt.android
    annotationProcessor libs.hilt.compiler

    // Glide (Image Loading)
    implementation libs.glide
    annotationProcessor libs.glide.compiler

    // Room (Local Database)
    implementation libs.room.runtime
    annotationProcessor libs.room.compiler

    // Gson
    implementation libs.gson

    // CircleImageView
    implementation libs.circleimageview

    // SwipeRefreshLayout
    implementation libs.swiperefreshlayout

    // Google Play Services (Maps & Location)
    implementation libs.playservices.maps
    implementation libs.playservices.location

    // New Places SDK (replaces deprecated play-services-places)
    implementation libs.places

}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.19.1').aosp().reflowLongStrings()

        // Remove unused imports
        removeUnusedImports()

        // Trim trailing whitespace
        trimTrailingWhitespace()

        // End files with a newline
        endWithNewline()

        // Custom formatting rules - you can add more here
        importOrder('android', 'androidx', 'com', 'junit', 'net', 'org', 'java', 'javax', '')

        // Optional: Use a custom rules file (create spotless.xml in your project root)
        // eclipseFormatFile('spotless.xml')
    }

    format 'xml', {
        target 'src/**/*.xml'
        trimTrailingWhitespace()
        endWithNewline()
        indentWithSpaces(4)
    }

    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
